plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

javafx {
    version = '22'
    modules = ['javafx.controls', 'javafx.media']
    configuration = 'compileOnly'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

version = "1.1.2"

dependencies {
    compileOnly "org.lifecompanion:lc-app:$lifecompanionAppVersion"

    implementation 'io.ultreia:bluecove:2.1.1'
    implementation 'org.json:json:20240303'
}

clean {
    delete 'build'
}

task buildApp {
    description = "Builds the APK and copies it to the resources directory"

    doLast {
        def androidDir = file('android')
        exec {
            workingDir = androidDir
            if (System.getProperty('os.name').toLowerCase().contains('windows')) {
                commandLine "./gradlew.bat", ":app:assembleDebug"
            } else {
                commandLine "./gradlew", ":app:assembleDebug"
            }
        }

        def apkSource = file('android/app/build/outputs/apk/debug/app-debug.apk')
        def apkDestination = file('src/main/resources/apk/lc-service.apk')

        if (!apkSource.exists()) {
            throw new GradleException("APK file not found: ${apkSource}")
        }

        apkDestination.parentFile.mkdirs()

        copy {
            from apkSource
            into apkDestination.parent
            rename { 'lc-service.apk' }
        }

        println "APK successfully built and copied to ${apkDestination}"
    }
}

jar {
    duplicatesStrategy DuplicatesStrategy.WARN

    manifest {
        attributes(
                "LifeCompanion-Plugin-Class": "org.lifecompanion.plugin.phonecontrol.PhoneControlPlugin",
                "LifeCompanion-Plugin-Package-Scanning-Base": "org.lifecompanion.plugin.phonecontrol",
                "LifeCompanion-Plugin-Id": "lc-phonecontrol-plugin",
                "LifeCompanion-Plugin-Author": "Etudiants IUT Vannes",
                "LifeCompanion-Plugin-Version": project.version,
                "LifeCompanion-Plugin-Build-Date": new Date().getTime(),
                "LifeCompanion-Plugin-Name": "Contrôle de téléphone",
                "LifeCompanion-Plugin-Description": "Ajoute à LifeCompanion la possibilité de controller son téléphone via l'application comme l'envoi, la lecture, et la réception de SMS et le passage d'appels.",
                "LifeCompanion-Min-App-Version": "$lifecompanionAppVersion"
        )
    }

    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}
