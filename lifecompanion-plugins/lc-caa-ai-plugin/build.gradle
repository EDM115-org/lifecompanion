plugins {
    id 'java-library'
    id 'org.openjfx.javafxplugin' version '0.1.0'
    id 'com.gradleup.shadow' version '8.3.0'
}

javafx {
    version = '22'
    modules = ['javafx.controls', 'javafx.media']
    configuration = 'compileOnly'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

version = "0.1.0"

dependencies {
    compileOnly "org.lifecompanion:lc-app:$lifecompanionAppVersion"
    implementation 'com.google.cloud:google-cloud-speech:4.41.0'
    // If you need to add plugin dependencies, add them with "implementation"
    // Note that you should check that the dependency is not already integrated in LifeCompanion core (if so, use "compileOnly" instead)
}

// Enter your plugin meta data
// When changing ID : think also about changing it everywhere in your code
jar {
    duplicatesStrategy DuplicatesStrategy.WARN
    manifest {
        attributes(
                "LifeCompanion-Plugin-Class": "org.lifecompanion.plugin.caaai.CAAAIPlugin",
                "LifeCompanion-Plugin-Package-Scanning-Base": "org.lifecompanion.plugin.caaai",
                "LifeCompanion-Plugin-Id": "lc-caa-ai-plugin",
                "LifeCompanion-Plugin-Author": "LifeCompanion AAC",
                "LifeCompanion-Plugin-Version": project.version,
                "LifeCompanion-Plugin-Build-Date": new Date().getTime(),
                "LifeCompanion-Plugin-Name": "CAA-AI - LifeCompanion LLM",
                "LifeCompanion-Plugin-Description": "Plugin du projet CAA-AI : utilisation de LLM au sein de LifeCompanion",
                "LifeCompanion-Min-App-Version": "$lifecompanionAppVersion"
        )
    }
    from(configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
}
